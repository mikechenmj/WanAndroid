static def runDependencies(DependencyHandler dependencyHandler, Object module) {
    module["implementation"].each {
        dependencyHandler.implementation it
    }
    module["api"].each {
        dependencyHandler.api it
    }
    module["testImplementation"].each {
        dependencyHandler.testImplementation it
    }
    module["androidTestImplementation"].each {
        dependencyHandler.androidTestImplementation it
    }
    module["debugImplementation"].each {
        dependencyHandler.debugImplementation it
    }
    module["releaseImplementation"].each {
        dependencyHandler.releaseImplementation it
    }
    module["kapt"].each {
        dependencyHandler.kapt it
    }
    module["androidTestApi"].each {
        dependencyHandler.androidTestApi it
    }
    module["testApi"].each {
        dependencyHandler.testApi it
    }

    module["project"].each {
        Map<String, String> project = new HashMap<>()
        project.put("path", it)
        dependencyHandler.add("implementation", dependencyHandler.project(project))
    }
}

ext {
    ext.runDependencies = this.&runDependencies


    app = [
            "implementation"       : [
                    leakcanary,
            ],
            "debugImplementation"  : [
                    blockcanary,
            ],
            "releaseImplementation": [
                    blockcanary_no_op,
            ],
            "kapt"                 : [
                    lifecycle_kapt,
            ],
            "project"              : [
                    ":lib_base",
                    ":lib_network",
                    ":lib_common",
                    ":data_content",
                    ":data_user",
                    ":feature_home",
                    ":feature_tree",
            ]
    ]

    feature_home = [
            "project": [
                    ":lib_base",
                    ":data_content",
                    ":lib_common",
                    ":lib_network",
            ]
    ]

    feature_tree = [
            "project": [
                    ":lib_base",
                    ":data_content",
                    ":lib_common",
                    ":lib_network",
            ]
    ]

    data_content = [
            "project": [
                    ":lib_base",
                    ":lib_common",
                    ":lib_network",
            ]
    ]

    data_user = [
            "project": [
                    ":lib_base",
                    ":lib_common",
                    ":lib_network",
            ]
    ]

    lib_base = [
            "api"    : [
                    material,
                    constraintlayout,
                    recyclerview,
                    lifecycle_runtime,
                    lifecycle_runtime_ktx,
                    lifecycle_livedata_ktx,
                    lifecycle_viewmodel_ktx,
                    lifecycleProcess,
                    fragment_ktx,
                    activity_ktx,
                    easypermissions,
                    databinding_runtime,
                    mmkv,
                    glide,
                    paging,
                    flex,
                    navigation_fragment_ktx,
                    navigation_ui_ktx,
                    navigation_dynamic_features_fragment,
            ],
            "project": [
                    ":lib_common",
                    ":lib_network",
            ]
    ]

    lib_network = [
            "api"    : [
                    retrofit2,
                    retrofit2_converter_gson,
                    retrofit2_adapter_rxjava,
                    retrofit2_converter_scalars,
                    okhttp3_logging_interceptor,
                    retrofit2_converter_moshi,
                    moshi_kotlin,
            ],
            "project": [
                    ":lib_common"
            ]
    ]

    lib_common = [
            "implementation": [
                    startup,
            ],
            "api"           : [
                    core_ktx,
                    appcompat,
                    coroutines_core,
                    coroutines_android,
                    annotation,
                    legacy,
                    utilcode,
            ],
            "testApi"       : [
                    junit,
            ],
            "androidTestApi": [
                    extjunit,
                    espresso,
            ],
    ]
}
